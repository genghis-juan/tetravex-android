import org.dbtools.gen.android.AndroidObjectsBuilder

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'org.dbtools:dbtools-gen:2.2.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    mavenCentral()
}

dependencies {
    annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.2'
    // includes support-v4
    implementation 'org.dbtools:dbtools-android:2.2.3'
    implementation 'com.squareup.dagger:dagger:1.2.2'
    implementation 'joda-time:joda-time:2.4'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId 'com.github.tetravex_android'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

task dbtools {
    description = 'Generate DBTools domain classes'
    doLast {
        System.out.println("Generating DBTools Classes...")

        // properties
        AndroidObjectsBuilder builder = new AndroidObjectsBuilder()
        builder.setXmlFilename("src/main/database/schema.xml")
        builder.setOutputBaseDir("src/main/java/com/github/tetravex_android/domain")
        builder.setPackageBase("com.github.tetravex_android.domain")
        builder.setInjectionSupport(true)
        builder.setJsr305Support(true)
        builder.setDateTimeSupport(true)
        builder.setEncryptionSupport(false)
        builder.build()
    }
}
